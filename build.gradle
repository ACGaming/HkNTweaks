buildscript {
    repositories {
        maven {
            url = "https://maven.minecraftforge.net"
        }
        maven {
            name = "Sponge"
            url = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath 'com.anatawa12.forge:ForgeGradle:2.3-1.0.7'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "1.12.2-1.6.0"
group = "mod.acgaming.universaltweaks"
archivesBaseName = "UniversalTweaks"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"

    def args = ["-Dmixin.debug.export=true",
                '-Dmixin.hotSwap=true',
                '-Dmixin.checks.interfaces=true']

    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'mod.acgaming.universaltweaks.core.UTLoadingPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'ForceLoadAsMod': 'true'
        attributes 'FMLAT': 'universaltweaks_at.cfg'
    }
}

repositories {
    maven {
        name = "Curse Maven"
        url "https://www.cursemaven.com"
    }
    maven {
        name = "CleanroomMC"
        url "https://maven.cleanroommc.com"
    }
    maven {
        name = "Sponge"
        url = "https://repo.spongepowered.org/maven"
    }
    maven {
        name = "DVS1 Maven FS"
        url = "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "jaredlll08 Maven"
        url = "https://maven.blamejared.com"
    }
}

dependencies {
    deobfCompile "curse.maven:abyssalcraft-53686:3425234"
    deobfCompile "curse.maven:baubles-227083:2518667"
    deobfCompile "curse.maven:binnies-mods-223525:2916129"
    deobfCompile "curse.maven:biomes-o-plenty-220318:2842510"
    deobfCompile "curse.maven:botania-225643:3330934"
    deobfCompile "curse.maven:chameleon-230497:2450900"
    deobfCompile "curse.maven:chickens-241941:2537643"
    deobfCompile "curse.maven:cqrepoured-303422:3953103"
    deobfCompile "curse.maven:elementary-staffs-346007:2995593"
    deobfCompile "curse.maven:elenaidodge2-442962:3343308"
    deobfCompile "curse.maven:epic-siege-mod-229449:3356157"
    deobfCompile "curse.maven:fluxnetworks-248020:3178199"
    deobfCompile "curse.maven:forestry-59751:2918418"
    deobfCompile "curse.maven:modtweaker-220954:3840577"
    deobfCompile "curse.maven:roost-277711:2702080"
    deobfCompile "curse.maven:storage-drawers-223852:2952606"
    deobfCompile "curse.maven:thaumcraft-223628:2629023"
    deobfCompile "curse.maven:thermal-expansion-69163:2926431"
    deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.684"
    deobfCompile "com.teamacronymcoders:ContentTweaker:1.12.2-4.10.0"
    deobfCompile "com.teamacronymcoders.base:base:1.12.2-3.14.0"
    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.3.55"
    deobfCompile "slimeknights:TConstruct:1.12.2-2.13.0.183"
    deobfCompile "zone.rong:mixinbooter:7.0"
    implementation "curse.maven:custom-mob-spawner-229261:2859433"
    implementation "com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2"
    //implementation "net.jafama:jafama:2.3.2"
}

mixin {
    add sourceSets.main, "universaltweaks.refmap.json"
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed "com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2"
}

jar {
    from(configurations.embed.collect {
        it.isDirectory() ? it : zipTree(it)
    })
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}
